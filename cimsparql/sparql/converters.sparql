# Name: Converters
PREFIX cim:<${cim}>
PREFIX pti:<${pti}>
PREFIX xsd:<${xsd}>
select ?mrid ?name ?alias ?p ?q ?substation_mrid ?status ?node ?connectivity_node ?area
where {
  # Extract active and reactive power for the converter
  ?converter cim:ACDCConverter.p ?p;
             cim:ACDCConverter.q ?q .

  optional {?converter ^cim:SvStatus.ConductingEquipment/cim:SvStatus.inService ?in_service .}

  # Extract connected and optionally the mrid for the topological node associated with the terminal
  ?terminal cim:ACDCTerminal.connected ?connected .
  optional {?terminal cim:Terminal.TopologicalNode/cim:IdentifiedObject.mRID ?term_top_node_mrid .}

  # Extract the mrid of the topoligical node associated with the connectivity node for the terminal
  optional {?con_node cim:ConnectivityNode.TopologicalNode/cim:IdentifiedObject.mRID ?con_top_node_mrid .}

  ?_eq_subject <http://entsoe.eu/CIM/EquipmentCore/3/1> ?eq_repo .
  service ?eq_repo {
  # Extract mrid, name, substation and optionally aliasName of the converter
  ?converter cim:IdentifiedObject.mRID ?mrid;
             cim:IdentifiedObject.name ?name;
             cim:Equipment.EquipmentContainer/cim:DCConverterUnit.Substation ?substation .
  optional {?converter cim:IdentifiedObject.aliasName ?alias}

    # Find area and mRID for the substation
    ?substation cim:Substation.Region/cim:SubGeographicalRegion.Region/cim:IdentifiedObject.name ?area;
		cim:IdentifiedObject.mRID ?substation_mrid .

    # Extract properties for the terminals for the converter
    ?terminal cim:Terminal.ConductingEquipment ?converter;
              cim:Terminal.ConnectivityNode ?con_node .
    ?con_node cim:IdentifiedObject.mRID ?connectivity_node .
    filter regex(?area, '${region}')
  }
  optional {?converter pti:Equipment.excludeFromCase ?_exclude_from_case} .
  bind(coalesce(?_exclude_from_case, false) as ?exclude_from_case)
  filter (!?exclude_from_case)


  # Asiign an mrid to the ?node variable. The first that exists is choden
  # 1) mRID of the topological node associated with the connectivity node
  # 2) mRID of the topological node for the terminal
  # 3) mRID of the terminal
  bind(coalesce(?con_top_node_mrid, ?term_top_node_mrid) as ?node)
  bind(coalesce(?in_service, ?connected) as ?status)
}
