# Name: Loads
PREFIX cim: <${cim}>
PREFIX pti:<${pti}>
select ?mrid ?name ?node ?status ?p ?q ?station_group ?connectivity_node
where {

  # Extrcact connected flag and optionally the mRID of the topological node associated with the terminal
  ?terminal cim:ACDCTerminal.connected ?connected .
  optional {?terminal cim:Terminal.TopologicalNode/cim:IdentifiedObject.mRID ?term_top_node_mrid}

  # Optionally extract the mRID of the topological node for the connectivity node associated with each terminal
  optional {?con_node cim:ConnectivityNode.TopologicalNode/cim:IdentifiedObject.mRID ?con_top_node_mrid .}

  # Optionally extract in_service flag and active and reactive energy consumption for each load
  optional {?load ^cim:SvStatus.ConductingEquipment/cim:SvStatus.inService ?in_service }.
  ?load cim:EnergyConsumer.p ?p; cim:EnergyConsumer.q ?q .

  ?_eq_subject <http://entsoe.eu/CIM/EquipmentCore/3/1> ?eq_repo .
  service ?eq_repo {
  # Extract mRID, name and substation for each load
  ?load a cim:EnergyConsumer;
         cim:IdentifiedObject.mRID ?mrid;
         cim:IdentifiedObject.name ?name;
         cim:Equipment.EquipmentContainer/cim:VoltageLevel.Substation ?substation.

  # Extract area and mRID (referred to as 'station') for the substation of each load
  ?substation cim:Substation.Region/cim:SubGeographicalRegion.Region/cim:IdentifiedObject.name ?area.

  filter regex(?area, '${region}')

  # Optionally extract bidzone for each substation and station_group for each load
  optional {?load pti:Equipment.excludeFromCase ?_exclude_from_case} .
  bind(coalesce(?_exclude_from_case, false) as ?exclude_from_case)
  filter (!?exclude_from_case)
  optional {?load cim:NonConformLoad.LoadGroup/pti:NonConformLoadGroup.ScheduleResource/pti:ScheduleResource.marketCode ?station_group} .

  # Extract mRID and connectivity node for each terminal associated with a load
  ?terminal cim:Terminal.ConductingEquipment ?load;
           cim:Terminal.ConnectivityNode ?con_node .
  ?con_node cim:IdentifiedObject.mRID ?connectivity_node .
  }

  # Assign status flag for load. The status flag is set to the first existing of
  # 1) in_service flag
  # 2) connected flag the terminal
  bind(coalesce(?in_service, ?connected) as ?status)

  # Assign mRID to the ?node variable. The mRID is set to the first existing of
  # 1) mRID of the topoligical node associated with a connectivity node for the terminal for each load
  # 2) mRID of the topological node associated with the terminal for each load
  bind(coalesce(?con_top_node_mrid, ?term_top_node_mrid) as ?node)
  filter(?node)
}
