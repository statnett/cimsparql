# Name: Converter HVDC bidzones
PREFIX cim:<${cim}>
PREFIX pti:<${pti}>
PREFIX entsoe2:<${entsoe2}>
PREFIX entsoe:<${entsoe}>
select distinct ?bidzone_1 ?bidzone_2 ?converter_1 ?converter_2 ?name_1 ?name_2
where {
  ?line a cim:DCLineSegment;
        cim:IdentifiedObject.name ?name.

  ?terminal_1 cim:DCTerminal.DCConductingEquipment ?line;
              cim:Terminal.sequenceNumber 1;
              cim:DCBaseTerminal.DCNode/^cim:DCBaseTerminal.DCNode/cim:ACDCConverterDCTerminal.DCConductingEquipment ?conducting_equipment_1.
  ?conducting_equipment_1 ^cim:Terminal.ConductingEquipment/cim:Terminal.ConnectivityNode/cim:ConnectivityNode.ConnectivityNodeContainer/cim:VoltageLevel.Substation/pti:Substation.EnergySchedulingArea/entsoe2:EnergySchedulingArea.EnergyCongestionZone/entsoe:IdentifiedObject.energyIdentCodeEic ?bidzone_1;
			  cim:IdentifiedObject.mRID ?converter_1;
     cim:IdentifiedObject.name ?name_1.

  ?terminal_2 cim:DCTerminal.DCConductingEquipment ?line;
              cim:Terminal.sequenceNumber 2;
              cim:DCBaseTerminal.DCNode/^cim:DCBaseTerminal.DCNode/cim:ACDCConverterDCTerminal.DCConductingEquipment ?conducting_equipment_2.
  ?conducting_equipment_2 ^cim:Terminal.ConductingEquipment/cim:Terminal.ConnectivityNode/cim:ConnectivityNode.ConnectivityNodeContainer/cim:VoltageLevel.Substation/pti:Substation.EnergySchedulingArea/entsoe2:EnergySchedulingArea.EnergyCongestionZone/entsoe:IdentifiedObject.energyIdentCodeEic ?bidzone_2;
			  cim:IdentifiedObject.mRID ?converter_2;
     cim:IdentifiedObject.name ?name_2.

  filter (?bidzone_1 != ?bidzone_2)
}
