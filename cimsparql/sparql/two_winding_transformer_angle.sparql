# Name: Two winding transformer angle
PREFIX cim: <${cim}>
PREFIX rdf: <${rdf}>
PREFIX xsd: <${xsd}>
select ?mrid ?angle
where {
  ?winding1 cim:PowerTransformerEnd.PowerTransformer ?p_transformer;
            cim:IdentifiedObject.mRID ?mrid;
            cim:TransformerEnd.endNumber 1 .
  ?winding2 cim:PowerTransformerEnd.PowerTransformer ?p_transformer;
            cim:TransformerEnd.endNumber 2 .
  ?tap_changer cim:PhaseTapChangerLinear.stepPhaseShiftIncrement ?inc;
               cim:TapChanger.normalStep ?normalstep;
               cim:PhaseTapChanger.TransformerEnd ?winding2 .
  service <${repo}> {
    ?tap_changer cim:TapChanger.step ?ssh_position .
    optional{ ?tap_changer ^cim:SvTapStep.TapChanger/cim:SvTapStep.position ?sv_position }
    bind(coalesce(?sv_position, ?ssh_position) as ?position)
  }

  bind((xsd:double(str(?position)) - xsd:double(str(?normalstep))) * xsd:double(str(?inc)) as ?angle)
}
