# Name: Transformers
PREFIX cim: <${cim}>
PREFIX rdf: <${rdf}>
PREFIX SN: <${SN}>
select  ?name ?p_mrid ?w_mrid ?endNumber ?un ?t_mrid ?r ?x ?rate
where {
  # Extract mRID for each power transformer
  ?p_transformer rdf:type cim:PowerTransformer;
           cim:IdentifiedObject.mRID ?p_mrid;
           cim:Equipment.EquipmentContainer/cim:Substation.Region/cim:SubGeographicalRegion.Region/cim:IdentifiedObject.name ?area .
  ?winding cim:PowerTransformerEnd.PowerTransformer ?p_transformer;
           cim:PowerTransformerEnd.ratedU ?un;
           cim:PowerTransformerEnd.r ?r;
           cim:PowerTransformerEnd.x ?x;
           cim:IdentifiedObject.mRID ?w_mrid;
           cim:IdentifiedObject.name ?name;
           cim:TransformerEnd.endNumber ?endNumber;
           cim:TransformerEnd.Terminal/cim:IdentifiedObject.mRID ?t_mrid;

  filter regex(?area, '${region}') .
  optional {
    ?p_lim cim:OperationalLimit.OperationalLimitSet/cim:OperationalLimitSet.Equipment ?winding;
           rdf:type cim:ActivePowerLimit;
           cim:IdentifiedObject.name '${rate}';
           cim:ActivePowerLimit.value ?rate
  }
  optional {?p_transformer SN:Equipment.networkAnalysisEnable ?_analysis_enabled .}
  filter(?analysis_enabled)
  bind(coalesce(?_analysis_enabled, True) as ?analysis_enabled)
}
