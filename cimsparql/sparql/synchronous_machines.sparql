# Name: Synchronous machines
PREFIX cim: <${cim}>
PREFIX SN: <${SN}>
select ?mrid ?name ?market_code ?node ?status ?station_group ?station_group_name ?substation_mrid ?maxP ?minP ?MO ?bidzone ?sn ?p ?q ?connectivity_node
where {
  ?terminal cim:ACDCTerminal.connected ?connected .
  optional {?terminal cim:Terminal.TopologicalNode/cim:IdentifiedObject.mRID ?term_top_node_mrid}
  optional {?con_node cim:ConnectivityNode.TopologicalNode/cim:IdentifiedObject.mRID ?con_top_node_mrid .}
  optional {?machine ^cim:SvStatus.ConductingEquipment/cim:SvStatus.inService ?in_service} .
  optional {?machine cim:RotatingMachine.p ?p;cim:RotatingMachine.q ?q .}

  # Extract properties for synchronous machines.
  ?_eq_subject <http://entsoe.eu/CIM/EquipmentCore/3/1> ?eq_repo .
  service ?eq_repo {
    ?machine a cim:SynchronousMachine;
          cim:IdentifiedObject.name ?name;
          cim:RotatingMachine.ratedS ?sn;
          cim:IdentifiedObject.mRID ?mrid;
          ^cim:Terminal.ConductingEquipment ?terminal;
          cim:Equipment.EquipmentContainer/cim:VoltageLevel.Substation ?substation .
    ?terminal cim:Terminal.ConnectivityNode ?con_node .
    ?con_node cim:IdentifiedObject.mRID ?connectivity_node .

    # Extract area and mrid for the substation associated with a sync machine
    ?substation cim:Substation.Region/cim:SubGeographicalRegion.Region/cim:IdentifiedObject.name ?area;
                cim:IdentifiedObject.mRID ?substation_mrid .

    # Optionally extract min/max operating power for each machin
    optional {
      ?machine cim:SynchronousMachine.GeneratingUnit ?gen_unit.
      ?gen_unit cim:GeneratingUnit.minOperatingP ?minP;
                cim:GeneratingUnit.maxOperatingP ?maxP .
      optional {
        ?gen_unit SN:GeneratingUnit.marketCode ?market_code;
                  SN:GeneratingUnit.groupAllocationWeight ?MO;
                  SN:GeneratingUnit.ScheduleResource ?ScheduleResource .
        ?ScheduleResource SN:ScheduleResource.marketCode ?station_group;
                          cim:IdentifiedObject.name ?station_group_name .
      }
    }
    filter regex(?area, '${region}')

    # Optionally extract bidzone for the substation associated with the sync machin
    optional {
      ?substation SN:Substation.MarketDeliveryPoint/SN:MarketDeliveryPoint.BiddingArea/SN:BiddingArea.marketCode ?bidzone
    } .

    # Opionally extract non-CIM standard properties generating units
    optional {?machine SN:Equipment.networkAnalysisEnable ?_network_analysis}
  }
  bind(coalesce(?_network_analysis, True) as ?network_analysis)
  bind(coalesce(?con_top_node_mrid, ?term_top_node_mrid) as ?node)
  bind(coalesce(?in_service, ?connected) as ?status)
  filter(?network_analysis)
}
