# Name: Connectivity nodes

PREFIX cim:<${cim}>
PREFIX SN:<${SN}>
PREFIX entsoeSecretariat:<${entsoeSecretariat}>

select ?mrid (?container_mrid as ?container) ?container_name ?un ?bidzone ?container_type
where {
   ?con_node cim:ConnectivityNode.ConnectivityNodeContainer ?con_node_container;
      cim:IdentifiedObject.mRID ?mrid .

  # Extract properties from the connectivity node container associated with each topoligical node
  {
    ?con_node_container cim:VoltageLevel.BaseVoltage/cim:BaseVoltage.nominalVoltage ?un ;
    	cim:VoltageLevel.Substation ?_substation .

    # Extract information about the substation associated with the connectivity node container
    ?_substation cim:IdentifiedObject.mRID ?container_mrid;
      cim:IdentifiedObject.name ?container_name;
      cim:Substation.Region/cim:SubGeographicalRegion.Region/cim:IdentifiedObject.name ?area .
     # Extract the bidzone of each substation if it exists
    optional {
      ?_substation SN:Substation.MarketDeliveryPoint/SN:MarketDeliveryPoint.BiddingArea/entsoeSecretariat:IdentifiedObject.energyIdentCodeEIC ?direct_bidzone
    } .
    {
      filter(!bound(?bidzone))
      # When an EIC code does not exist, look for an EIC code in neighbouring nodes
      {
      select (max(?related_bidzone) as ?nearby_bidzone) where {
          # Collect bidzones from substations connected to the current substation via a ConductingEquipment
        ?con_node ^cim:Terminal.ConnectivityNode/cim:Terminal.ConductingEquipment/^cim:Terminal.ConductingEquipment/cim:Terminal.ConnectivityNode/cim:ConnectivityNode.ConnectivityNodeContainer/cim:VoltageLevel.Substation/SN:Substation.MarketDeliveryPoint/SN:MarketDeliveryPoint.BiddingArea/entsoeSecretariat:IdentifiedObject.energyIdentCodeEIC ?related_bidzone .
          }
      }
    }
    bind("Substation" as ?container_type)
    bind(coalesce(?direct_bidzone, ?nearby_bidzone) as ?bidzone)
  } union {
    ?con_node_container cim:Line.Region/cim:IdentifiedObject.name ?area ;
      cim:IdentifiedObject.mRID ?container_mrid;
      cim:IdentifiedObject.name ?container_name
      bind("Line" as ?container_type)
  }
  FILTER regex(?area, '.*')
}
