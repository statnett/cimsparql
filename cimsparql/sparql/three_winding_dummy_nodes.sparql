# Name: Three winding with dummy nodes
PREFIX rdf:<${rdf}>
PREFIX cim:<${cim}>
PREFIX SN:<${SN}>
select ?node ?name (?name as ?busname) ?un ?station ?bidzone
where
{
  # Select all three winding transformerss
  {
    select  ?p_transformer (count(distinct ?nr) as ?winding_count)
    where {
      ?p_transformer rdf:type cim:PowerTransformer;
               cim:Equipment.EquipmentContainer/cim:Substation.Region/cim:SubGeographicalRegion.Region/cim:IdentifiedObject.name ?area;
               ^cim:PowerTransformerEnd.PowerTransformer/cim:TransformerEnd.endNumber ?nr.
      filter(regex(?area, '${region}'))
    }
    group by ?p_transformer
    having (?winding_count = 3)
  } .
  ?p_transformer cim:IdentifiedObject.mRID ?node;
           cim:IdentifiedObject.name ?name;
           cim:Equipment.EquipmentContainer ?Substation .
  ?winding cim:TransformerEnd.endNumber 1;
         cim:PowerTransformerEnd.ratedU ?un;
         cim:PowerTransformerEnd.PowerTransformer ?p_transformer .
  ?Substation cim:IdentifiedObject.mRID ?station .
  optional {?Substation SN:Substation.MarketDeliveryPoint/SN:MarketDeliveryPoint.BiddingArea/SN:BiddingArea.marketCode ?bidzone}
  optional {?p_transformer SN:Equipment.networkAnalysisEnable ?_network_analysis}
  filter(?network_analysis)
  bind(coalesce(?_network_analysis, True) as ?network_analysis)
}
