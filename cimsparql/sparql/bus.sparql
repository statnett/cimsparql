# Name: Bus
PREFIX cim:<${cim}>
PREFIX SN:<${SN}>
select ?node ?name ?busname ?un ?station ?bidzone
where {
  # Extract propertoes from the TP/SV/SSH profile for each topological node
  service <${repo}> {
    ?top_node a cim:TopologicalNode;
           cim:IdentifiedObject.mRID ?node;
           cim:IdentifiedObject.name ?busname;
           cim:TopologicalNode.BaseVoltage ?base_voltage;
           cim:TopologicalNode.ConnectivityNodeContainer ?container .
  }

  # Find the nominal voltage from the base voltage associated with the topological node
  ?base_voltage cim:BaseVoltage.nominalVoltage ?un .

  # Extract properties from the connectivity node container associated with each topoligical node
  ?container cim:IdentifiedObject.name ?name;
             cim:VoltageLevel.Substation ?substation .

  # Extract information about the substation associated with the connectivity node container
  ?substation cim:IdentifiedObject.mRID ?station;
              cim:Substation.Region/cim:SubGeographicalRegion.Region/cim:IdentifiedObject.name ?area .

  # Extract the bidzone of each substation if it exists
  optional {?substation SN:Substation.MarketDeliveryPoint/SN:MarketDeliveryPoint.BiddingArea/SN:BiddingArea.marketCode ?bidzone} .
  FILTER regex(?area, '${region}')
}
