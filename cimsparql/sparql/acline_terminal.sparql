# Name: AC Line Terminal
PREFIX cim: <${cim}>
PREFIX SN: <${SN}>
PREFIX xsd: <${xsd}>
select ?acline (?connected as ?connected_${nr}) (?p as ?p_${nr}) ?node_${nr} (?bidzone as ?bidzone_${nr})
where
{
  # Extract properties for all terminals where conducting equipment is of type ACLineSegment
  service <${eq_repo}> {
  ?acline a cim:ACLineSegment .
  ?terminal a cim:Terminal;
           cim:IdentifiedObject.mRID ?t_mrid;
           cim:Terminal.ConductingEquipment ?acline;
           cim:Terminal.ConnectivityNode ?con_node;
           cim:Terminal.sequenceNumber|cim:ACDCTerminal.sequenceNumber ${nr} .

  # Find substation of each connectivity node of the terminals above
  ?con_node cim:ConnectivityNode.ConnectivityNodeContainer/cim:VoltageLevel.Substation ?substation .

  # Find area and optionally bidzone for each substation
  ?substation cim:Substation.Region/cim:SubGeographicalRegion.Region/cim:IdentifiedObject.name ?area .
  optional {
    ?substation SN:Substation.MarketDeliveryPoint/SN:MarketDeliveryPoint.BiddingArea/SN:BiddingArea.marketCode ?bidzone .
  }
  filter(regex(?area, '${region}'))
  }

  # Find the mRID for the toplogical node of each connectivity node
  optional { ?con_node cim:ConnectivityNode.TopologicalNode/cim:IdentifiedObject.mRID ?con_top_node_mrid .}

  # Find properties in TP/SV/SSH profile for each terminal.
  ?terminal cim:Terminal.connected|cim:ACDCTerminal.connected ?connected .
  optional {?terminal cim:Terminal.TopologicalNode/cim:IdentifiedObject.mRID ?term_top_node_mrid} .
  optional {?terminal ^cim:SvPowerFlow.Terminal/cim:SvPowerFlow.p ?p .}
  optional {?acline SN:Equipment.networkAnalysisEnable ?_analysis_enabled .}
  filter(?analysis_enabled)
  bind(coalesce(?_analysis_enabled, True) as ?analysis_enabled)

  # Assign an mrid to the ?node_<nr> variable. The mrid is set to the first that exist
  # 1) mRID of the topological node of the connectivity node for each terminal
  # 2) mRID of the topological node of each terminal
  # 3) mrID of the terminal
  bind(coalesce(?con_top_node_mrid, ?term_top_node_mrid, ?t_mrid) as ?node_${nr})
}
