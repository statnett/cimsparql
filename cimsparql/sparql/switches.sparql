# Name: Switches
PREFIX cim: <${cim}>
select ?mrid ?is_open (strafter(str(?type), "#") as ?equipment_type) ?connectivity_node_1 ?connectivity_node_2 where {
	{
        select * {
            ?_eq_subject <http://entsoe.eu/CIM/EquipmentCore/3/1> ?eq_repo .
            service ?eq_repo {
                values ?type {cim:Breaker cim:Disconnector}
                ?switch cim:IdentifiedObject.mRID ?mrid;
                        a ?type;
                        cim:Switch.normalOpen ?normal_open;
                        ^cim:Terminal.ConductingEquipment ?terminal1;
                        ^cim:Terminal.ConductingEquipment ?terminal2 .
            ?terminal1 cim:ACDCTerminal.sequenceNumber 1;
                        cim:Terminal.ConnectivityNode/cim:IdentifiedObject.mRID ?connectivity_node_1 .
            ?terminal2 cim:ACDCTerminal.sequenceNumber 2;
                        cim:Terminal.ConnectivityNode/cim:IdentifiedObject.mRID ?connectivity_node_2 .
            }
        }
    }
    optional{?switch cim:Switch.open ?ssh_is_open}
    bind(coalesce(?ssh_is_open, ?normal_open) as ?is_open)
}
