# Name: Busbar Section
PREFIX cim: <http://iec.ch/TC57/2013/CIM-schema-cim16#>
SELECT DISTINCT ?mrid WHERE {
  {
    SELECT ?busbar WHERE {
      ?busbar a cim:BusbarSection .
      ?con_node ^cim:Terminal.ConnectivityNode/cim:Terminal.ConductingEquipment ?busbar .
      ?other_terminals cim:Terminal.ConnectivityNode ?con_node .
    }
    # Remove busbars that only connect two other terminal in addition to itself.
    GROUP BY ?busbar
    HAVING (COUNT(?other_terminals) > 3)
  }
  ?con_node ^cim:Terminal.ConnectivityNode/cim:Terminal.ConductingEquipment ?busbar ;
            cim:IdentifiedObject.mRID ?mrid ;
}
