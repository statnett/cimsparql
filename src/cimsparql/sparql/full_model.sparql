# Name: Full model
PREFIX md: <http://iec.ch/TC57/61970-552/ModelDescription/1#>
SELECT ?model ?time ?profile ?version ?description WHERE {
  {
    ?model md:Model.profile ?profile ;
           md:Model.scenarioTime ?time ;
           md:Model.version ?version ;
           md:Model.description ?description .
    {
      SELECT ?model (MIN(?dependency_exists) AS ?all_dependencies_exist) {
        ?_eq_subject <http://entsoe.eu/CIM/EquipmentCore/3/1> ?eq_repo .
        ?model md:Model.DependentOn ?depend_on .
        # Extract created time from both tp/sv/ssh profile and eq.
        OPTIONAL {
          {
            ?depend_on md:Model.created ?created
          }
          UNION {
            SERVICE ?eq_repo {
              ?depend_on md:Model.created ?created
            }
          }
        }
        BIND (BOUND(?created) AS ?dependency_exists)
      }
      GROUP BY ?model
    }
    FILTER (?all_dependencies_exist)
  }
  UNION {
    ?_eq_subject <http://entsoe.eu/CIM/EquipmentCore/3/1> ?eq_repo .
    SERVICE ?eq_repo {
      ?model md:Model.description ?description ;
             md:Model.created ?time .
      OPTIONAL {
        ?model md:Model.profile ?nullable_profile
      }
      OPTIONAL {
        ?model md:Model.version ?nullable_version
      }
      BIND (COALESCE(?nullable_profile, "http://entsoe.eu/CIM/EquipmentCore/3/1") AS ?profile)
      BIND (COALESCE(?nullable_version, "") AS ?version)
    }
  }
}
