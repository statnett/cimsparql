# Name: Converter HVDC bidzones
PREFIX cim: <http://iec.ch/TC57/2013/CIM-schema-cim16#>
PREFIX SN: <http://www.statnett.no/CIM-schema-cim15-extension#>
PREFIX ALG: <http://www.alstom.com/grid/CIM-schema-cim15-extension#>
SELECT DISTINCT ?mrid ?bidzone_1 ?bidzone_2 WHERE {
  ?converter ALG:DCConverter.DCPole | ALG:VoltageSourceConverter.DCPole ?pole ;
             cim:IdentifiedObject.mRID ?mrid .
  ?pole cim:Equipment.EquipmentContainer/SN:Substation.MarketDeliveryPoint ?delivery_point .
  ?delivery_point SN:MarketDeliveryPoint.BiddingArea/SN:BiddingArea.marketCode ?bidzone_1 .
  # Extract the destination point from the line that has one end matching bidzone_1
  ?line SN:Line.FromMarketDeliveryPoint ?delivery_point ;
        SN:Line.ToMarketDeliveryPoint/SN:MarketDeliveryPoint.BiddingArea/SN:BiddingArea.marketCode ?bidzone_2
  FILTER (?bidzone_1 != ?bidzone_2)
}
