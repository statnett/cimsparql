# Name: Associated switches
PREFIX cim: <http://iec.ch/TC57/2013/CIM-schema-cim16#>
SELECT (SAMPLE(?equipment_mrid) AS ?mrid)
       (SAMPLE(?name) AS ?name)
       (GROUP_CONCAT(?switch_mrid; SEPARATOR=',') AS ?switch_mrids)
       (GROUP_CONCAT(?switch_name; SEPARATOR=',') AS ?switch_names) WHERE {
  {
    ?equipment ^cim:Terminal.ConductingEquipment/cim:Terminal.ConnectivityNode/^cim:Terminal.ConnectivityNode/cim:Terminal.ConductingEquipment ?switch ;
               cim:IdentifiedObject.name ?name ;
               cim:IdentifiedObject.mRID ?equipment_mrid .
    ?switch cim:Switch.normalOpen ?_normalOpen ;
            cim:IdentifiedObject.mRID ?switch_mrid ;
            cim:IdentifiedObject.name ?switch_name ;
    # Only included switches that has a "second" terminal. There are switches
    # that only have one terminal. These switches can not connect/disconnect anything
            ^cim:Terminal.ConductingEquipment/cim:ACDCTerminal.sequenceNumber 2 .
  }
  MINUS {
    ?equipment cim:Switch.normalOpen ?_open
  }
}
GROUP BY ?equipment
