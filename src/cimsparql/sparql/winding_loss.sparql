# Name: Transformer branches loss
PREFIX cim: <http://iec.ch/TC57/2013/CIM-schema-cim16#>
PREFIX SN: <http://www.statnett.no/CIM-schema-cim15-extension#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?mrid (SUM(xsd:double(STR(?sv_p))) / SUM(?in_service) AS ?ploss_2) WHERE {
  ?terminal cim:ACDCTerminal.connected ?connected .
  OPTIONAL {
    ?terminal ^cim:SvPowerFlow.Terminal/cim:SvPowerFlow.p ?_sv_p .
  }
  OPTIONAL {
    ?winding ^cim:SvStatus.ConductingEquipment/cim:SvStatus.inService ?_in_service
  } .
  ?_eq_subject <http://entsoe.eu/CIM/EquipmentCore/3/1> ?eq_repo .
  SERVICE ?eq_repo {
    # Extract mRID and area for each power transformer
    ?winding cim:PowerTransformerEnd.PowerTransformer ?p_transformer ;
             cim:TransformerEnd.Terminal ?terminal .
    ?p_transformer cim:IdentifiedObject.mRID ?mrid ;
                   cim:Equipment.EquipmentContainer/cim:Substation.Region/cim:SubGeographicalRegion.Region/cim:IdentifiedObject.name ?area .
    FILTER (REGEX(?area,'.*'))
    OPTIONAL {
      ?p_transformer SN:Equipment.networkAnalysisEnable ?_network_analysis
    }
  }
  BIND (IF(COALESCE(?_in_service, ?connected),1,0) AS ?in_service) .
  BIND (COALESCE(?_sv_p, xsd:double(0.0)) AS ?sv_p)
  BIND (COALESCE(?_network_analysis, true) AS ?network_analysis)
  FILTER (?network_analysis)
}
GROUP BY ?mrid
