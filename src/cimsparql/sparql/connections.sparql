# Name: Connections
PREFIX cim: <http://iec.ch/TC57/2013/CIM-schema-cim16#>
PREFIX SN: <http://www.statnett.no/CIM-schema-cim15-extension#>
SELECT ?mrid ?t_mrid_1 ?t_mrid_2 WHERE {
  # Declare connector types
  VALUES ?connector_type { cim:Breaker cim:Disconnector }
  # Extract mRID for each connector
  ?connector a ?connector_type ;
             cim:IdentifiedObject.mRID ?mrid .
  # Extract properties for the terminal connected to sequenceNumber 1
  ?terminal1 cim:Terminal.ConductingEquipment ?connector ;
             cim:Terminal.sequenceNumber | cim:ACDCTerminal.sequenceNumber 1 ;
             cim:IdentifiedObject.mRID ?t_mrid_1 .
  # Extract properties for the terminal connected to sequenceNumber 2
  ?terminal2 cim:Terminal.ConductingEquipment ?connector ;
             cim:Terminal.sequenceNumber | cim:ACDCTerminal.sequenceNumber 2 ;
             cim:IdentifiedObject.mRID ?t_mrid_2
  OPTIONAL {
    ?connector SN:Equipment.networkAnalysisEnable ?_analysis_enabled .
  }
  BIND (COALESCE(?_analysis_enabled, True) AS ?analysis_enabled)
  FILTER (?analysis_enabled)
}
