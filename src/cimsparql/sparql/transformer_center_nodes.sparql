# Name: Transformer center nodes
PREFIX cim: <http://iec.ch/TC57/2013/CIM-schema-cim16#>
PREFIX SN: <http://www.statnett.no/CIM-schema-cim15-extension#>
PREFIX entsoeSecretariat: <http://entsoe.eu/Secretariat/ProfileExtension/1#>
SELECT ?node ?busname (?container_name AS ?substation) ?un ?base_voltage_mrid (?container_mrid AS ?substation_mrid) ?bidzone ?island ( False as ?is_swing_bus) ?connectivity_node (?un AS ?sv_voltage) WHERE {
  OPTIONAL {
    ?con_node cim:ConnectivityNode.TopologicalNode ?top_node .
    ?top_node ^cim:TopologicalIsland.TopologicalNodes/cim:IdentifiedObject.name ?island_name
  } .
  ?_eq_subject <http://entsoe.eu/CIM/EquipmentCore/3/1> ?eq_repo .
  SERVICE ?eq_repo {
    ?p_transformer cim:IdentifiedObject.mRID ?node ;
                   cim:IdentifiedObject.name ?busname ;
                   cim:Equipment.EquipmentContainer ?container .
    ?winding cim:TransformerEnd.endNumber 1 ;
             cim:PowerTransformerEnd.PowerTransformer ?p_transformer ;
             cim:TransformerEnd.Terminal ?terminal .
    ?terminal cim:Terminal.ConnectivityNode ?con_node .
    ?con_node cim:IdentifiedObject.mRID ?connectivity_node ;
              cim:ConnectivityNode.ConnectivityNodeContainer/cim:VoltageLevel.BaseVoltage ?base_voltage .
    ?container cim:IdentifiedObject.mRID ?container_mrid ;
               cim:IdentifiedObject.name ?container_name .
    ?container SN:Substation.MarketDeliveryPoint/SN:MarketDeliveryPoint.BiddingArea/entsoeSecretariat:IdentifiedObject.energyIdentCodeEIC ?bidzone
    OPTIONAL {
      ?p_transformer SN:Equipment.networkAnalysisEnable ?_network_analysis
    }
    ?base_voltage cim:IdentifiedObject.mRID ?base_voltage_mrid ;
                  cim:BaseVoltage.nominalVoltage ?un
  }
  FILTER (?network_analysis)
  BIND (COALESCE(?_network_analysis, True) AS ?network_analysis)
  BIND (COALESCE(?island_name, "Unknown") AS ?island)
}
