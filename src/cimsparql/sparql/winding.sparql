# Name: Windings
PREFIX cim: <http://iec.ch/TC57/2013/CIM-schema-cim16#>
PREFIX SN: <http://www.statnett.no/CIM-schema-cim15-extension#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?p_transformer
       (SUM(?_count) AS ?count)
       (MAX(?_name) AS ?name)
       (SUM(?in_service) AS ?nr_on)
       (SUM(xsd:double(STR(?sv_p))) AS ?p) WHERE {
  OPTIONAL {
    ?winding ^cim:SvStatus.ConductingEquipment/cim:SvStatus.inService ?_in_service .
  } .
  OPTIONAL {
    ?terminal cim:ACDCTerminal.connected ?_connected .
  }
  OPTIONAL {
    ?terminal ^cim:SvPowerFlow.Terminal/cim:SvPowerFlow.p ?_sv_p .
  }
  ?_eq_subject <http://entsoe.eu/CIM/EquipmentCore/3/1> ?eq_repo .
  SERVICE ?eq_repo {
    ?winding cim:PowerTransformerEnd.PowerTransformer ?p_transformer ;
             cim:TransformerEnd.Terminal ?terminal .
    ?p_transformer cim:IdentifiedObject.name ?_name .
  }
  BIND (COALESCE(?_connected, 1) AS ?connected)
  BIND (IF(COALESCE(?_in_service, ?connected),1,0) AS ?in_service) .
  BIND (COALESCE(?_sv_p, xsd:double(0.0)) AS ?sv_p)
  BIND (1 AS ?_count)
}
GROUP BY ?p_transformer
