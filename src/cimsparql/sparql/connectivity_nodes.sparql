# Name: Connectivity nodes
PREFIX cim: <http://iec.ch/TC57/2013/CIM-schema-cim16#>
PREFIX SN: <http://www.statnett.no/CIM-schema-cim15-extension#>
PREFIX entsoeSecretariat: <http://entsoe.eu/Secretariat/ProfileExtension/1#>
SELECT ?mrid (?container_mrid AS ?container) ?container_name ?un ?base_voltage_mrid ?bidzone ?container_type WHERE {
  ?con_node cim:ConnectivityNode.ConnectivityNodeContainer ?con_node_container ;
            cim:IdentifiedObject.mRID ?mrid .
  {
    SELECT ?con_node (COUNT(*) AS ?num_active) {
      # Support both typed and not typed networkAnalysisEnable flag
      VALUES ?true { True "true" }
      ?con_node ^cim:Terminal.ConnectivityNode/cim:Terminal.ConductingEquipment/SN:Equipment.networkAnalysisEnable ?true
    }
    GROUP BY ?con_node
  }
  FILTER (?num_active > 0)
  # Extract properties from the connectivity node container associated with each topological node
  {
    ?con_node_container cim:VoltageLevel.BaseVoltage ?base_voltage ;
                        cim:VoltageLevel.Substation ?_substation .
    ?base_voltage cim:BaseVoltage.nominalVoltage ?un ;
                  cim:IdentifiedObject.mRID ?base_voltage_mrid .
    # Extract information about the substation associated with the connectivity node container
    ?_substation cim:IdentifiedObject.mRID ?container_mrid ;
                 cim:IdentifiedObject.name ?container_name ;
                 cim:Substation.Region/cim:SubGeographicalRegion.Region/cim:IdentifiedObject.name ?area .
    # Extract the bidzone of each substation if it exists
    OPTIONAL {
      ?_substation SN:Substation.MarketDeliveryPoint/SN:MarketDeliveryPoint.BiddingArea/entsoeSecretariat:IdentifiedObject.energyIdentCodeEIC ?direct_bidzone
    } .
    {
      FILTER (!BOUND(?bidzone))
      # When an EIC code does not exist, look for an EIC code in neighbouring nodes
      {
        SELECT (MAX(?related_bidzone) AS ?nearby_bidzone) WHERE {
          # Collect bidzones from substations connected to the current substation via a ConductingEquipment
          ?con_node ^cim:Terminal.ConnectivityNode/cim:Terminal.ConductingEquipment/^cim:Terminal.ConductingEquipment/cim:Terminal.ConnectivityNode/cim:ConnectivityNode.ConnectivityNodeContainer/cim:VoltageLevel.Substation/SN:Substation.MarketDeliveryPoint/SN:MarketDeliveryPoint.BiddingArea/entsoeSecretariat:IdentifiedObject.energyIdentCodeEIC ?related_bidzone .
        }
      }
    }
    BIND ("Substation" AS ?container_type)
    BIND (COALESCE(?direct_bidzone, ?nearby_bidzone) AS ?bidzone)
  }
  UNION {
    ?con_node_container cim:Line.Region/cim:SubGeographicalRegion.Region/cim:IdentifiedObject.name ?area ;
                        cim:IdentifiedObject.name ?container_name ;
                        cim:IdentifiedObject.mRID ?container_mrid
    # Extract voltage levels connectivity nodes related via a conducting equipment (cim:Line has no cim:VoltageLevel.BaseVoltage)
    {
      SELECT ?base_voltage_mrid ?un {
        ?con_node ^cim:Terminal.ConnectivityNode/cim:Terminal.ConductingEquipment/^cim:Terminal.ConductingEquipment/cim:Terminal.ConnectivityNode/cim:ConnectivityNode.ConnectivityNodeContainer/cim:VoltageLevel.BaseVoltage ?base_voltage .
        ?base_voltage cim:IdentifiedObject.mRID ?base_voltage_mrid ;
                      cim:BaseVoltage.nominalVoltage ?un
      }
      ORDER BY DESC(xsd:double(STR(?un)))
      LIMIT 1
    }
    BIND ("Line" AS ?container_type)
  }
  FILTER REGEX(?area,'.*')
}
