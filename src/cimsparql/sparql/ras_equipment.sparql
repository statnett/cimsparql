# Name: RAS Equipment
PREFIX cim: <http://iec.ch/TC57/2013/CIM-schema-cim16#>
PREFIX alg: <http://www.alstom.com/grid/CIM-schema-cim15-extension#>
SELECT ?mrid ?equipment_mrid ?name ?flip ?collection WHERE {
  ?rpact (alg:ProtectiveActionEquipment.Equipment | alg:ProtectiveActionAdjustmentAC.ConductingEquipment | alg:ProtectiveActionAdjustmentDCController.DCController) ?equipment ;
         cim:IdentifiedObject.mRID ?mrid ;
         cim:IdentifiedObject.name ?name ;
         alg:ProtectiveAction.flowShift ?flow_shift ;
         alg:ProtectiveAction.flowShiftFlip ?flow_shift_flip ;
         alg:ProtectiveAction.loadContribution ?load_contribution ;
         alg:ProtectiveAction.ProtectiveActionCollection/cim:IdentifiedObject.mRID ?collection .
  ?equipment cim:IdentifiedObject.mRID ?equipment_mrid ;
  OPTIONAL {
    SELECT ?equipment (true AS ?is_branch) WHERE {
      VALUES ?type { cim:ACLineSegment cim:PowerTransformer cim:TransformerEnd }
      ?equipment a ?type .
    }
  }
  # Support both typed flow_shift and untyped
  BIND (BOUND(?is_branch) && (((STR(?flow_shift) = "true") && (STR(?flow_shift_flip) = "true")) || (STR(?load_contribution) = "true")) AS ?flip)
}
