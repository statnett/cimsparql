# Name: HVDC
PREFIX cim: <http://iec.ch/TC57/2013/CIM-schema-cim16#>
PREFIX ALG: <http://www.alstom.com/grid/CIM-schema-cim15-extension#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?converter_mrid_1
       ?converter_mrid_2
       (GROUP_CONCAT(?name; SEPARATOR="--") AS ?name)
       (SUM(xsd:float(STR(?r))) AS ?r) WHERE {
  ?converter_1 a ALG:VoltageSourceConverter ;
               cim:IdentifiedObject.mRID ?converter_mrid_1 .
  ?converter_2 a ALG:VoltageSourceConverter ;
               cim:IdentifiedObject.mRID ?converter_mrid_2 .
  ?t_1 cim:Terminal.ConnectivityNode/^cim:Terminal.ConnectivityNode/cim:Terminal.ConductingEquipment ?converter_1 ;
       cim:Terminal.ConductingEquipment ?line ;
       cim:ACDCTerminal.sequenceNumber 1 .
  ?t_2 cim:Terminal.ConnectivityNode/^cim:Terminal.ConnectivityNode/cim:Terminal.ConductingEquipment ?converter_2 ;
       cim:Terminal.ConductingEquipment ?line ;
       cim:ACDCTerminal.sequenceNumber 2 .
  ?line a ALG:DCLineSegment ;
        ALG:DCLineSegment.dcSegmentResistance ?r ;
        cim:IdentifiedObject.name ?name .
}
GROUP BY ?converter_mrid_1 ?converter_mrid_2
HAVING (COUNT(*) > 1)
